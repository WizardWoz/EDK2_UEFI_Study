;------------------------------------------------------------------------------
;
; Copyright (c) 2006 - 2022, Intel Corporation. All rights reserved.<BR>
; SPDX-License-Identifier: BSD-2-Clause-Patent
;
; Module Name:
;
;   EnablePaging64.Asm
;
; Abstract:
;
;   AsmEnablePaging64 function
;
; Notes:
;
;------------------------------------------------------------------------------

    SECTION .text

;------------------------------------------------------------------------------
; void
; __cdecl
; InternalX86EnablePaging64 (
;         UINT16                    Cs,
;         UINT64                    EntryPoint,
;         UINT64                    Context1,  
;         UINT64                    Context2,  
;         UINT64                    NewStack
;   );
;------------------------------------------------------------------------------
global _InternalX86EnablePaging64
_InternalX86EnablePaging64:
    cli
    mov     DWORD [esp], .0         ; offset for far retf, seg is the 1st arg
    mov     eax, cr4
    or      al, (1 << 5)
    mov     cr4, eax                    ; enable PAE
    mov     ecx, 0xc0000080
    rdmsr
    or      ah, 1                       ; set LME
    wrmsr
    mov     eax, cr0
    bts     eax, 31                     ; set PG
    mov     cr0, eax                    ; enable paging
    retf                                ; topmost 2 dwords hold the address
.0:
BITS 64
    mov     rbx, [esp]
    mov     rcx, [esp + 8]
    mov     rdx, [esp + 0x10]
    mov     rsp, [esp + 0x18]
    add     rsp, -0x20
    call    rbx
    hlt                                 ; no one should get here

